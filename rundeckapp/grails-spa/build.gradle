plugins {
  id "com.github.node-gradle.node"
  id 'base'
}

ext.spaBuildDir = "${buildDir}/spa"

configurations{
  spa
  uiTrellis {
      canBeConsumed(true)
  }
  uiTrellisVite {
      canBeConsumed(true)
  }
}

dependencies {
  uiTrellis project(path: ':rundeckapp:grails-spa:packages:ui-trellis', configuration: 'packages')
  uiTrellisVite project(path: ':rundeckapp:grails-spa:packages:ui-trellis-vite', configuration: 'packages')
}

node{
    // Whether to download and install a specific Node.js version or not
    // If false, it will use the globally installed Node.js
    // If true, it will download node using above parameters
    // Note that npm is bundled with Node.js
    download = false
    
    // Version of node to download and install (only used if download is true)
    // It will be unpacked in the workDir
    version = "16.13.0"
}



tasks.register("runNpmBuild", NpmTask) {

    inputs.file 'packages/ui-trellis-vite/package.json'
    inputs.file 'packages/ui-trellis-vite/package-lock.json'
    inputs.file 'packages/ui-trellis-vite/vite.config.app.js'
    inputs.file 'packages/ui-trellis-vite/tsconfig.json'
    inputs.file 'packages/ui-trellis-vite/.env.development'
    inputs.file 'packages/ui-trellis-vite/.env.production'

    inputs.dir 'packages/ui-trellis-vite/src'

    outputs.dir(file("$spaBuildDir"))
    outputs.cacheIf { true }

    def npmCommand = System.env.CI ?
      'ci:build:app' :
      'dev:build:app'

    args = ['run', npmCommand]

    execOverrides {
      it.workingDir = './packages/ui-trellis-vite'
    }
}



assemble.dependsOn runNpmBuild

artifacts {
  spa(file: file(spaBuildDir), name: "${project.name}", type: 'directory', builtBy: runNpmBuild)
}

